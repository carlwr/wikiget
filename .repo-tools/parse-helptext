#! /usr/bin/env zsh

setopt nounset pipefail

dedent() sed 's/^ *//'
mgrep () pcre2grep -O '$1' -M '(?sx)'${1:?}  

tagline     () mgrep '\A\s*         ([^\n]+)         .*\Z    '
examples    () mgrep '^EXAMPLES\n+  (.*)             \n+MISC$'
misc        () mgrep '^MISC\n+      (.*)             compdef '
misc_compdef() mgrep '^\s*          (compdef[^\n]+)  .*\Z    '


toReadme() {
############
## // README
cat <<EOF
# \`wikiget\`

_$(<<< $h tagline)_

Examples:

\`\`\`sh
$( <<< $h examples | dedent )
\`\`\`

$(<<< $h misc | dedent)

\`\`\`sh
$(<<< $h misc_compdef | dedent)
\`\`\`

## Requirements

* \`zsh\`, \`jq\`
* _optional:_ \`bat\`, \`pandoc\`

## \`wikiget --help\`

\`\`\`
${h}
\`\`\`

EOF
## README //
############
}


h=$(mgrep '\A (?:\n*) (.*)')  # capture from stdin

case $1 in
     (--getExamples)  <<< $h examples | dedent
  ;; (--toReadme   )  toReadme
  ;; (      *      )  >&2 echo "illegal option: $1"
  ;;
esac
